/**
 * @fileOverview Hash Trie.
 *  * 
 * Data structure can store any value except null.
 */
package(
    hash
    
    empty
    
    getHash
    get
    
    setHash
    set
    
    modifyHash
    modify
    
    removeHash
    remove)
{
/* Configuration
 ******************************************************************************/
var size = 4;

var mask = Math.pow(2, size) - 1;

/*
 ******************************************************************************/
var constant = \x -> \() -> x;

/* Nothing
 ******************************************************************************/
var nothing = null;

var isNothing = (===)@nothing;

/* Bit Ops
 ******************************************************************************/
var hashFragment = \shift h ->
    (h >>> shift) & mask;

/* 
 ******************************************************************************/
/**
 * Get 32 bit hash of string.
 * 
 * Based on:
 * http://stackoverflow.com/questions/7616461/generate-a-hash-from-string-in-javascript-jquery
 */
hash = \str -> {
    var hash = 0;
    for (var i = 0, len = str.length; i < len; i = i + 1)
    with char = str.charCodeAt(i) in {
        hash = (((hash << 5) - hash) + char) | 0;
    }
    return hash;
};

/* Node Structures
 ******************************************************************************/
/**
 * Empty node.
 */
empty = null;

/**
 * Leaf holding a value.
 * 
 * @member hash Hash of key.
 * @member key Key.
 * @member value Value stored.
 */
var Leaf = function \hash key value =self-> {
    self.hash = hash;
    self.key = key;
    self.value = value;
};

/**
 * Leaf holding multiple values with the same hash but different keys.
 * 
 * @member hash Hash of key.
 * @member list Array of key value pairs.
 */
var Collision = function \hash list =self-> {
    self.hash = hash;
    self.list = list;
};

/**
 * Internal node
 * 
 * @member count Number of children.
 * @member children Array of child nodes.
 */
var InternalNode = function \count children =self-> {
    self.count = count;
    self.children = children;
};

/* 
 ******************************************************************************/
var isEmpty = (!);

/**
 * Set a value in an array.
 * 
 * Returns copy of the array with value set.
 * 
 * @param at Index to set.
 * @param v Value.
 * @param arr Array.
 */
var arrayUpdate = \at v arr -> {
    var out = [];
    for (var i = 0, len = arr.length; i < len; i = i + 1)
    with e = arr.(i) in {
        if (!isEmpty e)
            out.(i) = e;
    }
    out.(at) = v;
    return out;
};

/**
 * Remove a value from an array.
 * 
 * @param at Index to remove.
 * @param arr Array.
 */
var arrayRemove = \at arr -> {
    var out = [];
    for (var i = 0, len = arr.length; i < len; i = i + 1)
        if (i !== at && !isEmpty arr.(i))
            out.(i) = arr.(i);
    return out;
};

/**
 * Create an 
 */
var createInternal = \pairs -> {
    var children = [];
    
    for (var i = 0, len = pairs.length; i < len; i = i + 1)
    with [frag node] = pairs.(i) in {
        children.(frag) = node;
    }
    
    return new InternalNode(pairs.length, children);
};

/**
 * Extract the first child node from an internal node.
 * 
 * @param removed Index of removed child.
 * @param children Subnodes.
 */
var getChild = \removed children -> {
    for (var i = 0, len = children.length; i < len; i = i + 1) {
        if (i !== removed && !isEmpty children.(i))
            return children.(i);
    }
    return empty;
};

/**
 * Merge two leaf nodes.
 * 
 * @param shift Current shift.
 * @param n1 Node.
 * @param n2 Node.
 */
var mergeLeaves = \shift n1 n2 ->
    ?isEmpty n2
        :n1 
        :let
            h1 = n1.hash,
            h2 = n2.hash
        in
            ?h1 === h2
                :new Collision(h1, [
                    [n2.key, n2.value],
                    [n1.key, n1.value]])
                :let
                    subH1 = hashFragment(shift, h1),
                    subH2 = hashFragment(shift, h2)
                in
                    createInternal(
                        ?subH1 === subH2
                            :[[subH1, mergeLeaves(shift + size, n1, n2)]]
                            :[[subH1, n1], [subH2, n2]]);

/**
 * Update an entry in a collision list.
 */
var updateCollisionList = \list f k ->
    ? !list.length
        :[]
        :let first = list.(0), rest = list.slice(1) in
            ?first.(0) === k
                :let v = f(first) in
                    ?isNothing v
                        :rest
                        :[v].concat(rest)
                :[first].concat(updateCollisionList(rest, f, k));

/* Lookups
 ******************************************************************************/
var lookup;

/**
 * Leaf::get
 */
Leaf.prototype.get = \_ _ k =self->
    ?k === self.key
        :self.value
        :nothing;

/**
 * Collision::get
 */
Collision.prototype.get = \_ _ k =self-> {
    for (var i = 0, len = self.list.length; i < len; i = i + 1)
    with [key value] = self.list.(i) in {
        if (k === key)
            return value;
    }
    return nothing;
};

/**
 * InternalNode::get
 */
InternalNode.prototype.get = \shift h k =self-> let
    frag = hashFragment(shift, h),
    child = self.children.(frag)
in
    lookup(shift + size, h, k, child);

lookup = \shift h k n ->
    ?isEmpty n
        :nothing
        :n.get(shift, h, k);

/* Editing
 ******************************************************************************/
var alter;

/**
 * empty::alter
 */
var alterEmpty = \_ f h k -> let
    v = f(nothing)
in
    ?isNothing v
        :empty
        :new Leaf(h, k, v);

/**
 * Leaf::modify
 */
Leaf.prototype.modify = \shift f h k =self->
    ?k === self.key
        :let v = f(self.value) in
            ?isNothing v
                :empty
                :new Leaf(h, k, v)
        :mergeLeaves(shift, self, alterEmpty(shift, f, h, k));

/**
 * Collision::modify
 */
Collision.prototype.modify = \shift f h k =self-> let
    list = updateCollisionList(self.list, f, k)
in
    ?list.length > 1
        :new Collision(self.hash, list)
        :new Leaf(h, list.(0).(0), list.(0).(1));

/**
 * InternalNode::modify
 */
InternalNode.prototype.modify = \shift f h k =self-> let
    frag = hashFragment(shift, h),
    child = self.children.(frag),
    newChild = alter(shift + size, f, h, k, child),
    removed = !isEmpty child && isEmpty newChild,
    added = isEmpty child && !isEmpty newChild
in
    ?added
        :new InternalNode(self.count + 1, arrayUpdate(frag, newChild, self.children))
        :?removed
            :?(self.count - 1) === 0
                :empty
                :new InternalNode(self.count - 1, arrayRemove(frag, self.children))
            :new InternalNode(self.count, arrayUpdate(frag, newChild, self.children));

alter = \shift f h k n ->
    ?isEmpty n
        :alterEmpty(shift, f, h, k)
        :n.modify(shift, f, h, k);

/* Single Updates
 ******************************************************************************/
/**
 * Lookup a value using the internal `hash` function to calculate the hash from `key`
 * 
 * Returns the value stored for the given hash an key or null if none.
 * 
 * @param h 32 bit hash.
 * @param k Key.
 * @param m HAMT
 */
getHash = \h k m ->
    lookup(0, h, k, m);

/**
 * Lookup a value using the internal `hash` function to calculate the hash from `key`
 * 
 * @see getHash
 */
get = \k m ->
    getHash(hash k, k, m);

/**
 * Modify the value stored for a hash.
 * 
 * Returns the modified data structure. The input `m` is not modified.
 * 
 * @param h 32 bit hash.
 * @param k Key.
 * @param f Function mapping current value to new value.
 * @param m HAMT
 */
modifyHash = \h k f m ->
    alter(0, f, h, k, m);

/**
 * Store a value using the internal `hash` function to calculate the hash from `key`
 * 
 * @see modifyHash
 */
modify = \k f m ->
    modifyHash(hash k, k, f, m);

/**
 * Store a value with an explicit hash.
 * 
 * @param h 32 bit hash.
 * @param k Key.
 * @param v Value to store.
 * @param m HAMT
 */
setHash = \h k v m ->
    modifyHash(h, k, constant v, m);

/**
 * Store a value using the internal `hash` function to calculate the hash from `key`
 * 
 * @see setHash
 */
set = \k v m ->
    setHash(hash k, k, v, m);

/**
 * Delete a value with an explicit hash.
 * 
 * @param m HAMT
 * @param h 32 bit hash.
 * @param k Key.
 */
removeHash = let del = constant nothing in
    \h k m ->
        modifyHash(h, k, del, m);

/**
 * Delete a value using the internal `hash` function to calculate the hash from `key`
 * 
 * @see removeHash
 */
remove = \k m ->
    removeHash(hash k, k, m);
}